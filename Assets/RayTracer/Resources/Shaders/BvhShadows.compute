#pragma kernel BvhShadows

// float4 _ZBufferParams;

#include "UnityCG.cginc"
#include "Bvh.cginc"
#include "Math.cginc"

Texture2D<float4> _MainTex;
Texture2D<float4> _CameraGBufferTexture2;
UNITY_DECLARE_TEX2D_FLOAT(_CameraDepthTexture);
RWTexture2D<float4> _TargetTex;
StructuredBuffer<Bvh::Node> _Nodes;
StructuredBuffer<IndexedTriangle> _Triangles;
StructuredBuffer<float4> _Vertices;
float3 _Light;
float2 _Size;
float4x4 _Projection;
float4x4 _InverseView;

#define STACK_SIZE 64

static const int _entrypointSentinel = 2147483647;
static const bool _anyHit = true;

struct FloatMinMax
{
	float min;
	float max;
};

float max4(float a, float b, float c, float d)
{
	return max(a, max(b, max(c, d)));
}

float min4(float a, float b, float c, float d)
{
	return min(a, min(b, min(c, d)));
}

// https://tavianator.com/fast-branchless-raybounding-box-intersections/
FloatMinMax IntersectAabb(Bvh::AABB b, Ray r)
{
	float3 invD = 1.0 / r.direction;
	float3 OoD = r.origin / r.direction;

	float x0 = b.min.x * invD.x - OoD.x;
	float y0 = b.min.y * invD.y - OoD.y;
	float z0 = b.min.z * invD.z - OoD.z;
	float x1 = b.max.x * invD.x - OoD.x;
	float y1 = b.max.y * invD.y - OoD.y;
	float z1 = b.max.z * invD.z - OoD.z;
 
 	FloatMinMax t;
    t.min = max4(0, min(x0, x1), min(y0, y1), min(z0, z1));
    t.max = min4(100000, max(x0, x1), max(y0, y1), max(z0, z1));
 
    return t;
}

int EncodeLeaf(int nodeIndex, bool isLeaf)
{
	if (isLeaf)
		nodeIndex = -(nodeIndex + 1);
	return nodeIndex;
}

int DecodeLeaf(int nodeIndex)
{
	return (-nodeIndex) - 1;
}



[numthreads(8, 8, 1)]
void BvhShadows(uint3 id : SV_DispatchThreadID)
{
	if ((float) id.x >= _Size.x || (float) id.y >= _Size.y)
		return;

	float zbufDpth = _CameraDepthTexture[id.xy].x;
	float vz = LinearEyeDepth(zbufDpth);
	float2 p11_22 = float2(_Projection._11, _Projection._22);
	float2 uv = ((float2)id.xy / _Size);
    float3 vpos = float3((uv * 2 - 1) / p11_22, -1) * vz;
    float4 wpos = mul(_InverseView, float4(vpos, 1));
    float3 normal = normalize(_CameraGBufferTexture2[id.xy] * 2 - 1);
	
    float3 direction = normalize(-_Light);
    Ray r = MakeRay(wpos + normal*1e-2, direction);

	int traversalStack[STACK_SIZE];
	traversalStack[0] = _entrypointSentinel;
	int nodeIndex = 0;
	int stackIndex = 0;

	// float3 idir = 1.0 / r.direction;
	float t = 100000;

	int boxIntersections = 0;
	int triangleIntersections = 0;
	int pushes = 0;
	int pops = 0;

	if (dot(direction, normal) <= 1e-3)
	{
		nodeIndex = _entrypointSentinel;
		t = 1;
	}

	while (nodeIndex != _entrypointSentinel)
	{
		while (nodeIndex >= 0 && nodeIndex != _entrypointSentinel)
		{
			Bvh::Node node = _Nodes[nodeIndex];
			FloatMinMax tLeft = IntersectAabb(node.leftBounds, r);
			FloatMinMax tRight = IntersectAabb(node.rightBounds, r);
			bool traverseLeft = tLeft.min <= tLeft.max    && tLeft.min  < t;
			bool traverseRight = tRight.min <= tRight.max && tRight.min  < t;

			int encodedLeft = EncodeLeaf(node.left, node.isLeftLeaf);
			int encodedRight = EncodeLeaf(node.right, node.isRightLeaf);
			nodeIndex = encodedLeft;

			if (traverseLeft != traverseRight)
			{
				boxIntersections++;
				// If only a single child was intersected we simply go to that one
				if (traverseRight)
					nodeIndex = encodedRight;
			}
			else
			{
				if (!traverseLeft)
				{
					// If neither child was intersected we pop the stack
					nodeIndex = traversalStack[stackIndex];
					stackIndex--;
					pops++;
				}
				else
				{
					boxIntersections += 2;
					// If both children were intersected we push one onto the stack
					nodeIndex = encodedLeft;
					int postponeIndex = encodedRight;
					if (tRight.min < tLeft.min)
					{
						nodeIndex = encodedRight;
						postponeIndex = encodedLeft;
					}

					stackIndex++;
					traversalStack[stackIndex] = postponeIndex;
					pushes++;
				}
			}
		}

		if (nodeIndex < 0)
		{
			nodeIndex = DecodeLeaf(nodeIndex);
			IndexedTriangle indices = _Triangles[nodeIndex];
			Triangle tri = MakeTriangle(_Vertices[indices.v1].xyz, _Vertices[indices.v2].xyz, _Vertices[indices.v3].xyz);

			// intersect triangle
			float3 candidateCoordinates;
			float candidate_t = IntersectTriangle(tri, r, candidateCoordinates); 
			triangleIntersections++;
			if (candidate_t > 0 && candidate_t < t)
			{
				t = candidate_t;
				break;
				// nodeIndex = _entrypointSentinel;

				// if (_anyHit)
				// 	break;
			}
			
			nodeIndex = traversalStack[stackIndex];
			stackIndex--;
		}
	}
	
	// just invert the colors
	//col = 1 - col;
	
	// fixed4(HeatMap(100, boxIntersections), 1);
	fixed4 col = _MainTex[id.xy]; // tex2D(_MainTex, i.uv);
	if (t < 99999)
		col *= 0.5;
	else
		col *= 1.0;
	// if (boxIntersections > 50-1)
	// 	col = fixed4(1, 0, 0, 1);
	_TargetTex[id.xy] = col;
}